ext.kotlin_version = '2.0.0'
allprojects {
    repositories {
        google()
        mavenCentral()
    }
    configurations.all {
        resolutionStrategy {
            force "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
            force "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
            force "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
            force "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        }
    }
}

subprojects { subproject ->
    subproject.buildscript {
        configurations.classpath.resolutionStrategy {
            force "org.jetbrains.kotlin:kotlin-gradle-plugin:${rootProject.ext.kotlin_version}"
        }
    }
    // Fix AGP 8: add namespace for plugins that don't have it
    subproject.afterEvaluate {
        if (subproject.plugins.hasPlugin('com.android.library')) {
            subproject.android {
                if (namespace == null || namespace.isEmpty()) {
                    def manifest = subproject.file('src/main/AndroidManifest.xml')
                    if (manifest.exists()) {
                        def pkg = new XmlParser().parse(manifest).attribute('package')
                        if (pkg) namespace = pkg
                    }
                }
            }
        }
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
